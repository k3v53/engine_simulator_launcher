import "engine_sim.mr"

units units()
constants constants()
impulse_response_library ir_lib()
label cycle(2 * 360 * units.deg)

private node wires {
    output wire1: ignition_wire();
    output wire2: ignition_wire();
    output wire3: ignition_wire();
    output wire4: ignition_wire();
}

private node honda_vtec_head {
    input camshaft_set;
    input chamber_volume: 41.6 * units.cc;
    input intake_runner_volume: 149.6 * units.cc;
    input intake_runner_cross_section_area: 1.35 * units.inch * 1.35 * units.inch;
    input exhaust_runner_volume: 50.0 * units.cc;
    input exhaust_runner_cross_section_area: 1.25 * units.inch * 1.25 * units.inch;

    input flow_attenuation: 1.0;
    input lift_scale: 1.0;
    input flip_display: false;
    alias output __out: head;

    function intake_flow(50 * units.thou)
    intake_flow
        .add_flow_sample(0 * lift_scale, 0 * flow_attenuation)
        .add_flow_sample(50 * lift_scale, 50 * flow_attenuation)
        .add_flow_sample(100 * lift_scale, 80 * flow_attenuation)
        .add_flow_sample(150 * lift_scale, 125 * flow_attenuation)
        .add_flow_sample(200 * lift_scale, 160 * flow_attenuation)
        .add_flow_sample(250 * lift_scale, 190 * flow_attenuation)
        .add_flow_sample(300 * lift_scale, 210 * flow_attenuation)
        .add_flow_sample(350 * lift_scale, 225 * flow_attenuation)
        .add_flow_sample(400 * lift_scale, 230 * flow_attenuation)
        .add_flow_sample(450 * lift_scale, 250 * flow_attenuation)

    function exhaust_flow(50 * units.thou)
    exhaust_flow
        .add_flow_sample(0 * lift_scale, 0 * flow_attenuation)
        .add_flow_sample(50 * lift_scale, 50 * flow_attenuation)
        .add_flow_sample(100 * lift_scale, 80 * flow_attenuation)
        .add_flow_sample(150 * lift_scale, 110 * flow_attenuation)
        .add_flow_sample(200 * lift_scale, 130 * flow_attenuation)
        .add_flow_sample(250 * lift_scale, 150 * flow_attenuation)
        .add_flow_sample(300 * lift_scale, 160 * flow_attenuation)
        .add_flow_sample(350 * lift_scale, 170 * flow_attenuation)
        .add_flow_sample(400 * lift_scale, 170 * flow_attenuation)
        .add_flow_sample(450 * lift_scale, 170 * flow_attenuation)

    generic_cylinder_head head(
        chamber_volume: chamber_volume,
        intake_runner_volume: intake_runner_volume,
        intake_runner_cross_section_area: intake_runner_cross_section_area,
        exhaust_runner_volume: exhaust_runner_volume,
        exhaust_runner_cross_section_area: exhaust_runner_cross_section_area,

        intake_port_flow: intake_flow,
        exhaust_port_flow: exhaust_flow,
        valvetrain: vtec_valvetrain(
            vtec_intake_camshaft: camshaft_set.vtec_intake_cam,
            vtec_exhaust_camshaft: camshaft_set.vtec_exhaust_cam,
            intake_camshaft: camshaft_set.intake_cam,
            exhaust_camshaft: camshaft_set.exhaust_cam
        ),
        flip_display: flip_display
    )
}

private node honda_vtec_camshaft {
    input lobe_profile;
    input intake_lobe_profile: lobe_profile;
    input exhaust_lobe_profile: lobe_profile;

    input vtec_lobe_profile;
    input vtec_intake_lobe_profile: vtec_lobe_profile;
    input vtec_exhaust_lobe_profile: vtec_lobe_profile;

    input lobe_separation: 114 * units.deg;
    input intake_lobe_center: lobe_separation;
    input exhaust_lobe_center: lobe_separation;
    input vtec_lobe_separation: 110 * units.deg;
    input vtec_intake_lobe_center: vtec_lobe_separation;
    input vtec_exhaust_lobe_center: vtec_lobe_separation;   
    input advance: 0 * units.deg; 
    input base_radius: 1.0 * units.inch;

    output intake_cam: _intake_cam;
    output exhaust_cam: _exhaust_cam;
    output vtec_intake_cam: _vtec_intake_cam;
    output vtec_exhaust_cam: _vtec_exhaust_cam;

    camshaft_parameters params (
        advance: advance,
        base_radius: base_radius
    )

    camshaft _intake_cam(params, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam(params, lobe_profile: exhaust_lobe_profile)
    camshaft _vtec_intake_cam(params, lobe_profile: vtec_intake_lobe_profile)
    camshaft _vtec_exhaust_cam(params, lobe_profile: vtec_exhaust_lobe_profile)

    label rot180(180 * units.deg)
    label rot360(360 * units.deg)

    _exhaust_cam
        .add_lobe(rot360 + exhaust_lobe_center - (0.0 / 4) * cycle)
        .add_lobe(rot360 + exhaust_lobe_center - (3.0 / 4) * cycle)
        .add_lobe(rot360 + exhaust_lobe_center - (1.0 / 4) * cycle)
        .add_lobe(rot360 + exhaust_lobe_center - (2.0 / 4) * cycle)
    _intake_cam
        .add_lobe(rot360 - intake_lobe_center - (0.0 / 4) * cycle)
        .add_lobe(rot360 - intake_lobe_center - (3.0 / 4) * cycle)
        .add_lobe(rot360 - intake_lobe_center - (1.0 / 4) * cycle)
        .add_lobe(rot360 - intake_lobe_center - (2.0 / 4) * cycle)
    _vtec_exhaust_cam
        .add_lobe(rot360 + vtec_exhaust_lobe_center - (0.0 / 4) * cycle)
        .add_lobe(rot360 + vtec_exhaust_lobe_center - (3.0 / 4) * cycle)
        .add_lobe(rot360 + vtec_exhaust_lobe_center - (1.0 / 4) * cycle)
        .add_lobe(rot360 + vtec_exhaust_lobe_center - (2.0 / 4) * cycle)
    _vtec_intake_cam
        .add_lobe(rot360 - vtec_intake_lobe_center - (0.0 / 4) * cycle)
        .add_lobe(rot360 - vtec_intake_lobe_center - (3.0 / 4) * cycle)
        .add_lobe(rot360 - vtec_intake_lobe_center - (1.0 / 4) * cycle)
        .add_lobe(rot360 - vtec_intake_lobe_center - (2.0 / 4) * cycle)
}

public node honda_vtec_i4 {
    alias output __out: engine;

    engine engine(
        name: "Honda B18C5 [VTEC, I4]",
        starter_torque: 70 * units.lb_ft,
        starter_speed: -500 * units.rpm,
        redline: 8400 * units.rpm,
        fuel: fuel(
            max_turbulence_effect: 2.5,
            max_burning_efficiency: 0.75
        ),
        throttle_gamma: 2.0,
        hf_gain: 0.002,
        noise: 0.253,
        jitter: 0.195,
        simulation_frequency: 20000
    )

    wires wires()

    label stroke(87.2 * units.mm)
    label bore(81 * units.mm)
    label rod_length(5.430 * units.inch)
    label compression_height(1.0 * units.inch)

    crankshaft c0(
        throw: stroke / 2,
        flywheel_mass: 10 * units.lb,
        mass: 35.5 * units.lb,
        friction_torque: 1.0 * units.lb_ft,
        moment_of_inertia: 0.22986844776863666 * 0.5,
        position_x: 0.0,
        position_y: 0.0,
        tdc: 90 * units.deg
    )

    rod_journal rj0(angle: 0.0 * units.deg)
    rod_journal rj1(angle: 180.0 * units.deg)
    rod_journal rj2(angle: 180.0 * units.deg)
    rod_journal rj3(angle: 0.0 * units.deg)
    c0
        .add_rod_journal(rj0)
        .add_rod_journal(rj1)
        .add_rod_journal(rj2)
        .add_rod_journal(rj3)

    piston_parameters piston_params(
        mass: 303.5 * units.g,
        compression_height: compression_height,
        wrist_pin_position: 0.0,
        displacement: 0.0
    )

    connecting_rod_parameters cr_params(
        mass: 395.837 * units.g,
        moment_of_inertia: 0.0015884918028487504,
        center_of_mass: 0.0,
        length: rod_length
    )

    intake intake(
        plenum_volume: 1.325 * units.L,
        plenum_cross_section_area: 20.0 * units.cm2,
        intake_flow_rate: k_carb(800.0),
        runner_flow_rate: k_carb(250.0),
        runner_length: 7.0 * units.inch,
        idle_flow_rate: k_carb(0.0),
        idle_throttle_plate_position: 0.9989,
        velocity_decay: 0.5
    )

    exhaust_system_parameters es_params(
        outlet_flow_rate: k_carb(1000.0),
        primary_tube_length: 10.0 * units.inch,
        primary_flow_rate: k_carb(200.0),
        velocity_decay: 1.0,
        volume: 100.0 * units.L
    )

    exhaust_system exhaust0(
        es_params,
        audio_volume: 8 * 0.75,
        impulse_response: ir_lib.mild_exhaust_0
    )

    exhaust_system exhaust1(
        es_params,
        audio_volume: 8 * 1.0,
        impulse_response: ir_lib.mild_exhaust_0
    )

    cylinder_bank_parameters bank_params(
        bore: bore,
        deck_height: stroke / 2 + rod_length + compression_height
    )

    cylinder_bank b0(bank_params, angle: 0.0 * units.deg)
    b0
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.001)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj0,
            intake: intake,
            exhaust_system: exhaust0,
            ignition_wire: wires.wire1,
            sound_attenuation: 0.9
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.002)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            intake: intake,
            exhaust_system: exhaust1,
            ignition_wire: wires.wire2,
            sound_attenuation: 1.1
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.001)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            intake: intake,
            exhaust_system: exhaust0,
            ignition_wire: wires.wire3,
            sound_attenuation: 0.8
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.002)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            intake: intake,
            exhaust_system: exhaust1,
            ignition_wire: wires.wire4,
            sound_attenuation: 0.9
        )

    engine
        .add_cylinder_bank(b0)

    engine.add_crankshaft(c0)

    harmonic_cam_lobe intake_lobe(
        duration_at_50_thou: 210 * units.deg,
        gamma: 1.0,
        lift: 6.9 * units.mm,
        steps: 100
    )

    harmonic_cam_lobe exhaust_lobe(
        duration_at_50_thou: 190 * units.deg,
        gamma: 1.0,
        lift: 6.5 * units.mm,
        steps: 100
    )

    harmonic_cam_lobe vtec_intake_lobe(
        duration_at_50_thou: 240 * units.deg,
        gamma: 0.5,
        lift: 11.5 * units.mm,
        steps: 100
    )

    harmonic_cam_lobe vtec_exhaust_lobe(
        duration_at_50_thou: 232 * units.deg,
        gamma: 0.5,
        lift: 10.5 * units.mm,
        steps: 100
    )

    honda_vtec_camshaft camshaft(
        lobe_profile: "N/A",
        vtec_lobe_profile: "N/A",

        intake_lobe_profile: intake_lobe,
        exhaust_lobe_profile: exhaust_lobe,
        vtec_intake_lobe_profile: vtec_intake_lobe,
        vtec_exhaust_lobe_profile: vtec_exhaust_lobe,
        intake_lobe_center: 116 * units.deg,
        exhaust_lobe_center: 116 * units.deg,
        vtec_intake_lobe_center: 100 * units.deg,
        vtec_exhaust_lobe_center: 100 * units.deg,
        base_radius: 500 * units.thou
    )

    b0.set_cylinder_head (
        honda_vtec_head(
            camshaft_set: camshaft
        )
    )

    function timing_curve(1000 * units.rpm)
    timing_curve
        .add_sample(0000 * units.rpm, -25 * units.deg)
        .add_sample(1000 * units.rpm, -25 * units.deg)
        .add_sample(2000 * units.rpm, -30 * units.deg)
        .add_sample(3000 * units.rpm, -40 * units.deg)
        .add_sample(4000 * units.rpm, -40 * units.deg)

    ignition_module ignition_module(
        timing_curve: timing_curve,
        rev_limit: 9400 * units.rpm, //43244374
        limiter_duration: 0.05)
    ignition_module
            .connect_wire(wires.wire1, (0.0 / 4.0) * cycle)
            .connect_wire(wires.wire3, (3.0 / 4.0) * cycle)
            .connect_wire(wires.wire4, (2.0 / 4.0) * cycle)
            .connect_wire(wires.wire2, (1.0 / 4.0) * cycle)

    engine.add_ignition_module(ignition_module)
}

private node integra_type_r {
    alias output __out:
        vehicle(
            mass: 2400 * units.lb,
            drag_coefficient: 0.2,
            cross_sectional_area: (66 * units.inch) * (50 * units.inch),
            diff_ratio: 3.55,
            tire_radius: 10 * units.inch,
            rolling_resistance: 300 * units.N
        );
}

private node integra_type_r_transmission {
    alias output __out:
        transmission(
            max_clutch_torque: 300 * units.lb_ft
        )
        .add_gear(3.23)
        .add_gear(2.105)
        .add_gear(1.458)
        .add_gear(1.107)
        .add_gear(0.848);
}

public node main {
    set_engine(honda_vtec_i4())
    set_vehicle(integra_type_r())
    set_transmission(integra_type_r_transmission())
}
